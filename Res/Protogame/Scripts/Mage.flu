/**
 * @Mage: A cool wizard
 * @Author: Tough guys!
 */
script Mage
{
private:
    bool            bJumping;
    bool            bCanJump;
    Book            myBook;
    
    
    event OnPreTick( float delta )
    {
        if( label != @Normal )
            return;
    
        // setup x-movement
        if( KeyIsPressed(KEY_Left) )
        {
            Velocity.X      = -11.0;
            $Sprite.bFlipH  = true;
        }
        else if( KeyIsPressed(KEY_Right) )
        {
            Velocity.X      = +11.0;
            $Sprite.bFlipH  = false;
        }                           
        else
            Velocity.X      = 0.0; 
            
            
        // setup jumps
        if( KeyIsPressed(KEY_Up) && bCanJump )
        {
            if( Floor != undefined )
            {
                Velocity.Y  = 25.0;
                bCanJump    = false;
                bJumping    = true;
                //PlaySoundFX( #SJump, 0.4, 1.0 );
            }
        }
        else if( !KeyIsPressed(KEY_Up) && bJumping )
        {
            bJumping    = false;
            bCanJump    = true;
            if( Velocity.Y > 0.0 )
                Velocity.Y = 0.0;
        }
        
        // apply gravity
        Velocity.Y  -= 65.0 * delta;
        if( Velocity.Y < -40.0 )
            Velocity.Y = -40.0;
              
        // which animation should we play?                        
        if( Floor != undefined )
        {
            if( Velocity.X != 0.0 )
                $Sprite.PlayAnim( "Walk", 12.0, ANIM_Loop );
            else
                $Sprite.PlayAnim( "Idle", 2.5, ANIM_Loop );
        }
        else
        {
            if( Velocity.y > 0.0 )
                $Sprite.PlayAnim( "JumpUp", 5.0, ANIM_Once );
            else
                 $Sprite.PlayAnim( "JumpDown", 2.0, ANIM_Once );           
        }
    }


    event OnCollide( entity Other, EHitSide Side )
    {
        if( Other is #Brush )
        {
            Brush B = Brush(Other);
            if( B.Type == BRUSH_Solid )
                SolveSolid( true );
            //else
            //    SolveOneway( true );                       
        }           
    }
    
    
    event OnBeginPlay()
    {
        //SetBlackEffect();
        //$Painter.PushEffect(0.0);
        //$Painter.PopEffect(1.5);

        bCanJump                    = true;

        Level level = Level;
        level.Camera.Location       = Location;
        level.Camera.Zoom           = 1.0;

        
        // ---
        myBook = new Book;
        myBook.init( this );
        
        goto @Normal;
    }
 
    event OnTick( float Delta )
    {       
        float thresh = 3.0; 

        Level level = Level;
        if( level.Camera.Location.X < Location.X-thresh ) level.Camera.Location.X = Location.X - thresh;
        if( level.Camera.Location.X > Location.X+thresh ) level.Camera.Location.X = Location.X + thresh;             
        if( level.Camera.Location.Y < Location.Y-thresh ) level.Camera.Location.Y = Location.Y - thresh;
        if( level.Camera.Location.Y > Location.Y+thresh ) level.Camera.Location.Y = Location.Y + thresh;   

        //if( Location.Y+Size.Y < Level.ScrollClamp.Min.Y )
        //    TakeDamage( this, "Fall", 999 );                                                               
    }
 
 
    event OnKeyDown( integer key )
    {
        if( key == KEY_K )
        {
            myBook.shoot( $Sprite.bFlipH ? -1.0 : 1.0 );
          
        }
        
        if( key == KEY_F && label == @Normal && nearestNPC != undefined )
        {
            goto @Talk;
        }
        
        if( key == KEY_F && label == @Talk )
        {
            dialog.ToNextMessage();
        }
    }
    
    
    event OnRender()
    {
        $Painter.Font = #YosterIsland21;
        $Painter.Color = rgba( 255, 255, 255, 255 );
        
        $Painter.TextOut( [16, 16], "Trololo " + string(Location), 1.0 );
        
        if( nearestNPC != undefined && label == @Normal )
            $Painter.TextOut( [16, 36], "Press <F> to enter chat...", 1.0 );  

        if( label == @Talk )
        {
            $Painter.Color = dialog.IsPlayerMessage() ? rgba(255, 64, 64, 255) : rgba( 64, 64, 255, 255 );
            string msg = dialog.GetCurrentMessage();
            
            $Painter.TextOut( [($Painter.Width - $Painter.TextSize(msg))/2, $Painter.Height - 30], msg, 1.0 ); 
        }                                     
    }    


    event OnBeginTouch( entity other )
    {
        if( other is #NPC )
            nearestNPC = NPC(other);
    }

    event OnEndTouch( entity other )
    {
        if( other == nearestNPC && label == @Normal )
            nearestNPC = undefined;
    }

private:
    NPC nearestNPC;
    Dialog dialog;

    thread
    {
    @Normal:
        stop;
    
    
    @Talk:
        Velocity = [0.0, 0.0];
        $Sprite.PlayAnim( "Jump_Outdated", 3.5, ANIM_Loop );
        dialog = nearestNPC.startTalk();
        dialog.Prepare();
       
       wait dialog.IsComplete();
       
       nearestNPC.finishTalk();       
       goto @Normal;
    
    }

}
