/**
 * @Book: A gliding wizard's book (weapon)
 * @Author: Tough guys
 */
script Book
{
public:
    Mage myMage;
    
    fn init( Mage mage )
    {
        myMage = mage;
        Layer = myMage.Layer + 0.01;
        goto @Idle;
    }
    
    fn shoot( float d )
    {
        if( label == @Idle )
        {
            dir = d;
            goto @Attack;
        }
    }

    event OnPreTick( float Delta )    
    {
        float minDist = 0.5;
        
        
        desiredY = myMage.Location.y;        
        float bookDir = myMage.$Sprite.bFlipH ? -1.0 : 1.0;
        float desiredX = myMage.Location.x + myMage.Size.x * 1.1 * bookDir;                      
         
        if( desiredX - Location.x > minDist )
        {
            Velocity.x = +11.0;
        }
        else if( desiredX - Location.x < -minDist )
        {
            Velocity.x = -11.0;
        }
        else
            Velocity.x = 0.0;
        
        
        SetLocation( [Location.x, desiredY + sin( now() * 1.0 ) * 1.5] );
        $Sprite.bFlipH = Location.x < myMage.Location.x;
    }

private:
    float desiredY;
    float dir;

    fn spawnProjectile()
    {
        Fireball f = new Fireball;
        f.Launch( dir );
    }


    thread
    {
    @Idle:
        $Sprite.PlayAnim( "Glide", 8.0, ANIM_Loop );
        stop;
        
    @Attack:
        $Sprite.PlayAnim( "Attack", 10.0, ANIM_Once );
        sleep 0.4;
        spawnProjectile();
        goto @Idle; 
    }
}
